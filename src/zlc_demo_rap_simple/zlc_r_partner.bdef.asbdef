managed implementation in class zbp_lc_r_partner unique;
strict ( 2 );

define behavior for ZLC_R_PARTNER alias Partner
persistent table zlc_dmo_partner
lock master
authorization master ( instance )
etag master LocalLastChangedAt
late numbering
{
  create ( authorization : global );
  update;
  delete;
  field ( readonly ) PartnerNumber, LocalCreatedBy, LocalLastChangedBy;

  // Validations
  validation validateKeyIsFilled on save { create; }
  validation validateCoreData on save { create; field Country, PaymentCurrency; }

  // Determinations
  determination fillCurrency on modify { create; update; }

  // Actions
  action fillEmptyStreets result [1] $self;
  static action ( authorization : global ) clearAllEmptyStreets;
  static action ( authorization : global ) withPopup parameter ZLC_I_PopupEntity;
  factory action copyLine [1];

  mapping for zlc_dmo_partner
    {
      PartnerNumber      = partner;
      PartnerName        = name;
      Street             = street;
      City               = city;
      Country            = country;
      PaymentCurrency    = payment_currency;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}